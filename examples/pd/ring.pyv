sort node

immutable relation le(node, node) @no_minimize

axiom le(X,X)
axiom le(X, Y) & le(Y, Z) -> le(X, Z)
axiom le(X, Y) & le(Y, X) -> X = Y
axiom le(X, Y) | le(Y, X)

immutable relation btw(node, node, node) @no_minimize

axiom btw(W,X,Y) & btw(W,Y,Z) -> btw(W,X,Z)
axiom btw(W,X,Y) -> !btw(W,Y,X)
axiom btw(W,X,Y) | btw(W,Y,X) | W=X | W=Y | X=Y
axiom btw(X,Y,Z) -> btw(Y,Z,X)

mutable relation leader(node)
mutable relation pending(node, node)

init !leader(N)
init !pending(N1, N2)

transition send(n: node, next: node)
  modifies pending
  (forall Z. n != next & ((Z != n & Z != next) -> btw(n,next,Z))) &
  (forall N1, N2. new(pending(N1, N2)) <-> pending(N1, N2) | N1 = n & N2 = next)

transition recv(sender: node, n: node, next: node)
  modifies leader, pending
  (forall Z. n != next & ((Z != n & Z != next) -> btw(n,next,Z))) &
  pending(sender, n) &
  (sender = n ->
    (forall N. new(leader(N)) <-> leader(N) | N = n) &
    (forall N1, N2.
      !(N1 = sender & N2 = n) ->  # message may or may not be removed
      (new(pending(N1, N2)) <-> pending(N1, N2)))) &
  (sender != n ->
    (forall N. new(leader(N)) <-> leader(N)) &
    (le(n, sender) ->
      (forall N1, N2.
        !(N1 = sender & N2 = n) ->  # message may or may not be removed
        (new(pending(N1, N2)) <-> pending(N1, N2) | N1 = sender & N2 = next))) &
    (!le(n, sender) ->
      (forall N1, N2.
        !(N1 = sender & N2 = n) ->  # message may or may not be removed
        (new(pending(N1, N2)) <-> pending(N1, N2)))))

safety [one_leader] leader(L1) & leader(L2) -> L1 = L2
invariant [leader_max] leader(L) -> le(N, L)
invariant [self_pending_highest_id] pending(L, L) -> le(N, L)
invariant [no_bypass] pending(S, D) & btw(S, N, D) -> le(N, S)
